import com.android.build.gradle.internal.LoggerWrapper
import com.android.build.gradle.internal.SdkHandler

apply plugin: 'java'
apply plugin: 'nexus'

def logger = new LoggerWrapper(project.logger)
def sdkHandler = new SdkHandler(project, logger)
for (File file : sdkHandler.sdkLoader.repositories) {
    project.repositories.maven {
        url = file.toURI()
    }
}

//noinspection GroovyAssignabilityCheck
configurations {
    bundled
    configurations.compile.extendsFrom(configurations.bundled)
}

dependencies {
    //noinspection GradleDependency
    compile 'com.android.support:support-annotations:23.4.0'
    compile project(':shiv-annotations')
    compile project(':shiv-core')
    compile 'com.squareup:javapoet:1.7.0'

    // Use bundled to include classes in jar, not as a dependency
    bundled 'android:support-v7-preference:23.4.0'
    bundled 'android:support-v4:23.4.0'
    bundled 'android:framework:23'
}

//noinspection GroovyAssignabilityCheck
jar {
    from {
        configurations.bundled.collect { it.isDirectory() ? it : zipTree(it) }
    }
}